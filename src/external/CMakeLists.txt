CMAKE_MINIMUM_REQUIRED(VERSION 2.8)

SET( ORIG_CMAKE_PREFIX_PATH ${CMAKE_PREFIX_PATH} )

SET( CMAKE_PREFIX_PATH ${CPPTEST_INSTALL_DIR} ${ORIG_CMAKE_PREFIX_PATH} )
FIND_PACKAGE(CPPTest REQUIRED)

SET( CMAKE_PREFIX_PATH ${LIBXML2_INSTALL_DIR} ${ORIG_CMAKE_PREFIX_PATH} )
FIND_PACKAGE(LibXml2 REQUIRED)

IF( NOT VSP_NO_GRAPHICS )
	SET( CMAKE_PREFIX_PATH ${FLTK_INSTALL_DIR} ${ORIG_CMAKE_PREFIX_PATH} )
	FIND_PACKAGE(FLTK REQUIRED)
	SET( FLTK_INCLUDE_DIR ${FLTK_INCLUDE_DIR} PARENT_SCOPE )
	SET( FLTK_LIBRARIES ${FLTK_LIBRARIES} PARENT_SCOPE )
	SET( FLTK_FLUID_EXECUTABLE ${FLTK_FLUID_EXECUTABLE} PARENT_SCOPE )
	SET( OPENGL_LIBRARIES ${OPENGL_LIBRARIES} PARENT_SCOPE )

	SET( CMAKE_PREFIX_PATH ${FLTK_INSTALL_DIR} ${FLTK_INSTALL_DIR}/include/FL/images ${ORIG_CMAKE_PREFIX_PATH} )

	SET( CMAKE_PREFIX_PATH ${GLM_INSTALL_DIR} ${ORIG_CMAKE_PREFIX_PATH} )
	FIND_PACKAGE( GLM )
	SET( GLM_INCLUDE_DIR  ${GLM_INCLUDE_DIR} PARENT_SCOPE )

	SET( CMAKE_PREFIX_PATH ${GEW_INSTALL_DIR} ${ORIG_CMAKE_PREFIX_PATH} )
	# Use built-in FindGLEW.cmake
	FIND_PACKAGE(GLEW_VSP REQUIRED)

	ADD_SUBDIRECTORY( glfont2 )

	ADD_SUBDIRECTORY( stb )

	ADD_SUBDIRECTORY( cartesian )
ENDIF()

# Add Eigen3 and Code-Eli
SET( CMAKE_PREFIX_PATH ${EIGEN_INSTALL_DIR} ${ORIG_CMAKE_PREFIX_PATH} )
FIND_PACKAGE(Eigen3 3.0.0 REQUIRED)
SET( EIGEN3_INCLUDE_DIR  ${EIGEN3_INCLUDE_DIR} PARENT_SCOPE )

SET(CMAKE_PREFIX_PATH ${CODEELI_INSTALL_DIR} ${ORIG_CMAKE_PREFIX_PATH} )
FIND_PACKAGE(CodeEli 0.3.6 REQUIRED)
SET( CodeEli_INCLUDE_DIRS  ${CodeEli_INCLUDE_DIRS} PARENT_SCOPE )

SET(CMAKE_PREFIX_PATH ${CMINPACK_INSTALL_DIR} ${ORIG_CMAKE_PREFIX_PATH} )
FIND_PACKAGE(CMinpack REQUIRED)
SET( CMINPACK_INCLUDE_DIR  ${CMINPACK_INCLUDE_DIR} PARENT_SCOPE )
SET( CMINPACK_LIBRARIES  ${CMINPACK_LIBRARIES} PARENT_SCOPE )


SET( CMAKE_PREFIX_PATH ${ORIG_CMAKE_PREFIX_PATH} )

ADD_SUBDIRECTORY( triangle )
ADD_SUBDIRECTORY( nanoflann )
ADD_SUBDIRECTORY( sixseries )
ADD_SUBDIRECTORY( tritri )
ADD_SUBDIRECTORY( clipper )

ADD_SUBDIRECTORY( wavedragEL )

INCLUDE( ExternalProject )
INCLUDE( External_STEPCode )

IF(NOT WIN32)
	set( STEPCODE_LIBRARIES
	${STEPCODE_INSTALL_DIR}/lib/libsdai_ap203.a
	${STEPCODE_INSTALL_DIR}/lib/libstepeditor.a
	${STEPCODE_INSTALL_DIR}/lib/libstepcore.a
	${STEPCODE_INSTALL_DIR}/lib/libstepdai.a
	${STEPCODE_INSTALL_DIR}/lib/libsteputils.a
	${STEPCODE_INSTALL_DIR}/lib/libbase.a
	PARENT_SCOPE )
ELSE()
	set( STEPCODE_LIBRARIES
	${STEPCODE_INSTALL_DIR}/lib/libexpress.lib
	${STEPCODE_INSTALL_DIR}/lib/libexppp.lib
	${STEPCODE_INSTALL_DIR}/lib/libsdai_ap203.lib
	${STEPCODE_INSTALL_DIR}/lib/libstepeditor.lib
	${STEPCODE_INSTALL_DIR}/lib/libstepcore.lib
	${STEPCODE_INSTALL_DIR}/lib/libstepdai.lib
	${STEPCODE_INSTALL_DIR}/lib/libsteputils.lib
	${STEPCODE_INSTALL_DIR}/lib/libbase.lib
	shlwapi.lib
	PARENT_SCOPE )
ENDIF()

MESSAGE( STATUS "STEPCODE_INSTALL_DIR: " ${STEPCODE_INSTALL_DIR} )

set( STEPCODE_INCLUDE_DIR
${STEPCODE_INSTALL_DIR}/include/stepcode
${STEPCODE_INSTALL_DIR}/include/stepcode/base
${STEPCODE_INSTALL_DIR}/include/stepcode/clstepcore
${STEPCODE_INSTALL_DIR}/include/stepcode/cldai
${STEPCODE_INSTALL_DIR}/include/stepcode/clutils
${STEPCODE_INSTALL_DIR}/include/stepcode/cleditor
${STEPCODE_INSTALL_DIR}/include/schemas/sdai_ap203
PARENT_SCOPE )


SET(CMAKE_PREFIX_PATH ${LIBIGES_INSTALL_DIR} ${ORIG_CMAKE_PREFIX_PATH} )

find_library(LIBIGES_LIBRARIES NAMES iges_static
    HINTS ${LIBIGES_INSTALL_DIR}/lib ${LIBIGES_INSTALL_DIR}/bin
)
SET( LIBIGES_LIBRARIES  ${LIBIGES_LIBRARIES} PARENT_SCOPE )

find_path(LIBIGES_INCLUDE_DIR iges/libigesconf.h
    HINTS ${LIBIGES_INSTALL_DIR}/include
)
SET( LIBIGES_INCLUDE_DIR  ${LIBIGES_INCLUDE_DIR} ${LIBIGES_INCLUDE_DIR}/iges ${LIBIGES_INCLUDE_DIR}/geom PARENT_SCOPE )
